# A graph of the price data presented in the daily announcements.  To generate
# the graph, run `gnuplot prices.graph`
#
# prices.data format:
# - lecture number (int)
# - BTC price / 1000 (USD, float)
# - ETH price / 1000 (USD, float)
# - gas price (gwei, float)
# - gas fees in (USD, float)
#
# - to generate the data, run (on Linux, not a Mac):
#   `grep gwei daily-announcements.html  | grep ETH | sed s/[\`,\;\$]//g | awk '{print 43-NR" "$3" "$5" "$8" "$11}' > prices.data`
# - then clean it up, fixing the lecture number (the first value of each
#   line), adjusting the scales (below in this file) as necessary, and change the date axis
# - change the other fields below (semester, dates, etc.)
# - run `make graph`

set xrange [1:43]
set yrange [0:75]
set key left top
set xtics ( "Jan 19" 2, "Jan 31" 7, "Feb 12" 12, "Feb 23" 17, "Mar 13" 22, "Mar 25" 27, "Apr 5 6" 32, "Apr 17" 37, "Apr 29" 42 )
set title "BTC and ETH prices & fees, spring 2024"
set xlabel "Date"
set ylabel "Price (USD)"
set terminal svg size 1000,500 background rgb 'white'
set output "prices.svg"
plot 'prices.data' using 1:($2/1000) with linespoints title "BTC / 1,000", 'prices.data' using 1:($3/100) with linespoints title "ETH / 100", \
	'prices.data' using 1:($5*8) with linespoints title "gas (USD) * 8", 'prices.data' using 1:($4/4) with linespoints title "gas (gwei) / 4"
